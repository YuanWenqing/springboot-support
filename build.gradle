buildscript {
    repositories {
        mavenLocal()
        maven {
            name "nexus"
            url "https://nexus.ilovelook.cn/repository/maven-public/"
            credentials {
                username property("nexus.username")
                password property("nexus.password")
            }
        }
    }
}

ext {
    springBootVersion = '2.0.3.RELEASE'
}

// project setting
subprojects {
    group = "top.fangwz.springboot"
    version = "0.2.0"

    repositories {
        mavenLocal()
        maven {
            name "nexus"
            url "https://nexus.ilovelook.cn/repository/maven-public/"
            credentials {
                username property("nexus.username")
                password property("nexus.password")
            }
        }
    }

    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "jacoco"

    jar << {
        println jar.archivePath
    }

    jacocoTestReport.dependsOn(test)
    jacocoTestReport << {
        println jacocoTestReport.reports.html.entryPoint
    }

    test << {
        println test.reports.html.entryPoint
    }

    apply plugin: "maven-publish"
    afterEvaluate {
        task sourcesJar(type: Jar) {
            from sourceSets.main.allJava
            classifier = 'sources'
        }
        publishing.publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId project.archivesBaseName
                version project.version

                artifact sourcesJar
            }
        }
        task local {
            dependsOn publishToMavenLocal
            doLast {
                def pub = publishing.publications.mavenJava
                println "artifact: ${pub.groupId}:${pub.artifactId}:${pub.version}"
            }
        }
    }


    dependencies {

        // Nullable & NonNull
        // http://spotbugs.readthedocs.io/en/latest/migration.html
        compileOnly('com.github.spotbugs:spotbugs-annotations:3.1.3')

        // test
        testImplementation('junit:junit:4.12')
    }
}